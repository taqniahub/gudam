'~~~~~~~~~~~~~~~~~~~~~~Calculator.Net Function Library~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Option Explicit
Dim MyBrowser
Set MyBrowser = Browser("micclass:=Browser").Page("micclass:=Page")


'Function to Enter Text Into a WebEdit
Function fnEnterText (Prop_Name, Prop_Class, Prop_HtmlTag, Prop_HtmlID, WE_Value)
	Dim oWebEdit
	Set oWebEdit = Description.Create
	oWebEdit("name").Value = Prop_Name
	oWebEdit("class").Value = Prop_Class
	oWebEdit("html tag").Value = Prop_HtmlTag
	oWebEdit("html id").Value = Prop_HtmlID
	
	'Action Line
	MyBrowser.WebEdit(oWebEdit).Set WE_Value
	
	'Housekeeping
	Set oWebEdit = Nothing
End Function


'Function to Click on an Image Button
Function fnClickImageButton (Prop_Alt, Prop_ImageType, Prop_HtmlTag, Prop_Index)
	Dim oImage
	Set oImage = Description.Create
	oImage("alt").Value = Prop_Alt
	oImage("image type").Value = Prop_ImageType
	oImage("html tag").Value = Prop_HtmlTag
	oImage("index").Value = Prop_Index
	
	'Action Line
	MyBrowser.Image(oImage).Click
	
	'Housekeeping
	Set oImage = Nothing
End Function

'Function to Select from a WebList
Function fnSelectList (Prop_Name, Prop_HtmlTag, Prop_HtmlID, WL_Value)
	Dim oWebList
	Set oWebList = Description.Create
	oWebList("name").Value = Prop_Name
	oWebList("html tag").Value = Prop_HtmlTag
	oWebList("html id").Value = Prop_HtmlID
	
	'Action Line
	MyBrowser.WebList(oWebList).Select cStr(WL_Value) 'Converting to string b/c weblist only accepts string values
	
	'Housekeeping
	Set oWebList = Nothing
End Function

'Function to Capture a Value from a WebTable
Function fnCaptureTable(Prop_ColNamesAsRegEx, Prop_HtmlTag, Prop_Class, Table_RowNum, Table_ColNum)
	Dim oWebTable, Captured_TableValue
	Set oWebTable = Description.Create
	oWebTable("column names").Value = Prop_ColNamesAsRegEx
	oWebTable("html tag").Value = Prop_HtmlTag
	oWebTable("class").Value = Prop_Class
	
	'Action Line
	Captured_TableValue = MyBrowser.WebTable(oWebTable).GetCellData(Table_RowNum, Table_ColNum)
	fnCaptureTable = Captured_TableValue 'Function Returns the value that was captured

	'Housekeeping
	Set oWebTable = Nothing
End Function

'Function to Compare 2 Values
Function fnCompare2Values(Value1, Value2)
	' This function will return a True or False, we will decide what to to with the True/False when we call the function
	If Value1 = Value2 Then
		fnCompare2Values = True
	Else
		fnCompare2Values = False	
	End If
End Function
'Scenario 1:
' If fnCompare2Values(10, 10) Then
'   Perform some actions
' else
'   Exit Test
' End if

'Scenario 2:
' If fnCompare2Values($1,000, $1,000)  Then
'   Print "Passed"
' Else
'   Print "Failed"
'End If

'Create a Function to Close Browsers
Function fnCloseBrowsers (Browser_Name)
	SystemUtil.CloseProcessByName Browser_Name
End Function

'Create a Function to Launch Browsers & Navigate to URL
Function fnLaunchApp (Browser_Name, URL)
	SystemUtil.Run Browser_Name, URL
End Function

Function fnWait (WaitTime)
	Wait WaitTime
End Function

Sub spEnterStartDates(From_Month, From_Day, From_Year)
	fnSelectList "select", "SELECT", "beginbalanceday_Month_ID", From_Month
	fnSelectList "select", "SELECT", "beginbalanceday_Day_ID", From_Day
	fnEnterText "WebEdit", "calendarDateInput", "INPUT", "beginbalanceday_Year_ID", From_Year
End Sub

Sub spEnterEndDates(To_Month, To_Day, To_Year)
	fnSelectList "select", "SELECT", "endbalanceday_Month_ID", To_Month
	fnSelectList "select", "SELECT", "endbalanceday_Day_ID", To_Day
	fnEnterText "WebEdit", "calendarDateInput", "INPUT", "endbalanceday_Year_ID", To_Year
End Sub

'Create a Function that will click on any WebElement

Function fnClickElement (Prop_Class, Prop_HtmlTag, Prop_InnerText)
	Dim oWebElement
	Set oWebElement = Description.Create
	oWebElement("class").Value = Prop_Class
	oWebElement("html tag").Value = Prop_HtmlTag
	oWebElement("innertext").Value = Prop_InnerText
	
	'Action Line
	MyBrowser.WebElement(oWebElement).Click
	
	'Housekeeping
	Set oWebElement = Nothing
End Function

'Create a Function to Capture a Value from a WebElement
Function fnCaptureElement (Prop_Class, Prop_HtmlTag, Prop_InnerTextAsRegEx, PropToCapture)
	Dim oWebElement, Captured_Value
	Set oWebElement = Description.Create
	oWebElement("class").Value = Prop_Class
	oWebElement("html tag").Value = Prop_HtmlTag
	oWebElement("innertext").Value = Prop_InnerTextAsRegEx
	
	'Action Line
	Captured_Value = MyBrowser.WebElement(oWebElement).GetROProperty(PropToCapture)
	
	fnCaptureElement = Captured_Value
	
	'Housekeeping
	Set oWebElement = Nothing

End Function
























